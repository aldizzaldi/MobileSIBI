plugins {
    id "com.github.dcendents.android-maven" version "2.0"
    id "com.jfrog.bintray" version "1.8.0"
    id "jacoco"
    id "com.github.kt3k.coveralls" version "2.5.0-x"
}
apply plugin: 'com.android.library'

def versionMajor = 0
def versionMinor = 0
def versionPatch = 5
def myVersionCode = versionMajor * 10000 + versionMinor * 100 + versionPatch
def myVersionName = "${versionMajor}.${versionMinor}.${versionPatch}"

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 27
        versionCode myVersionCode
        versionName myVersionName
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation 'com.android.support:support-annotations:27.1.0'
}

// https://stackoverflow.com/a/34572606/1251958
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
afterEvaluate {
    javadoc.classpath += files(android.libraryVariants.collect { variant ->
        variant.javaCompiler.classpath.files
    })
}

task jacocoTestReport(type: JacocoReport, dependsOn: "testDebugUnitTest") {
    reports {
        xml.enabled = true
        html.enabled = true
    }
    classDirectories = fileTree(
            dir: './build/intermediates/classes/debug',
            excludes: ['eu/darken/R*.class',
                       '**/BuildConfig.class'
            ])
    sourceDirectories = files([
            'src/main/java'
    ])
    executionData = files('build/jacoco/testDebugUnitTest.exec')
}

coveralls {
    jacocoReportPath = 'build/reports/jacoco/jacocoTestReport/jacocoTestReport.xml'
    sourceDirs = ['src/main/java']
}

def credentialsFile = new File(System.properties['user.home'], ".bintray/bintray.properties")
ext {
    if (credentialsFile.canRead()) {
        Properties props = new Properties()
        props.load(new FileInputStream(credentialsFile))
        bintrayUser = props['bintray.user']
        bintrayApiKey = props['bintray.apikey']
    }
    bintrayRepo = 'maven'
    bintrayName = 'myolib'

    publishedGroupId = 'eu.darken.myolib'
    libraryName = 'Android Myo library by darken'
    artifact = 'myolib'

    libraryDescription = 'An open source Android library to communicate with Myo devices.'

    siteUrl = 'https://github.com/d4rken/myolib'
    gitUrl = 'https://github.com/d4rken/myolib.git'
    issuesUrl = 'https://github.com/d4rken/myolib/issues'

    libraryVersion = myVersionName

    developerId = 'darken'
    developerName = 'Matthias Urhahn'
    developerEmail = 'darken@darken.eu'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}
if (credentialsFile.canRead()) apply from: '../publish-to-bintray.gradle'
